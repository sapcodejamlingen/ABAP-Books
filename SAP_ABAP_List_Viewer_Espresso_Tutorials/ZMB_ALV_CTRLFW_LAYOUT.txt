REPORT  zmb_alv_ctrlfw_layout.
*Espresso Tutorials - SAP ABAP List Viewer (ALV)

TABLES: sbook,
        stravelag.
*TYPE-POOLS slis.

TYPES: BEGIN OF lty_output,
        agencynum TYPE stravelag-agencynum, "agency number
        name      TYPE stravelag-name,          "agency name
        currency  TYPE stravelag-currency,   "agency currency
        carrid    TYPE sbook-carrid,           "booked carrier
        connid    TYPE sbook-connid,           "booked connection
        fldate    TYPE sbook-fldate,           "booked date
        bookid    TYPE sbook-bookid,           "booking ID
        forcuram  TYPE sbook-forcuram,       "price in foreign currency
        forcurkey TYPE sbook-forcurkey,     "foreign currency key
        carrname  TYPE scarr-carrname,        "carrier name
        loccuram  TYPE sbook-loccuram,       "price in airline curr
        loccurkey TYPE sbook-loccurkey,     "local currency of airline
       END OF lty_output.

DATA: gs_layout TYPE lvc_s_layo,
      gs_output   TYPE lty_output.            "local structure (line)
DATA: gt_output   TYPE STANDARD TABLE OF lty_output.
DATA: gt_fieldcat TYPE lvc_t_fcat.

DATA: gv_lines TYPE i,
      gv_title type syst-title,
      ok_code LIKE sy-ucomm,
      g_container TYPE scrfname VALUE 'ZMB_ALV_CTRLFW_9100_CONT1',
      grid1 TYPE REF TO cl_gui_alv_grid,
      g_custom_container TYPE REF TO cl_gui_custom_container.

SELECT-OPTIONS: s_agnum FOR stravelag-agencynum DEFAULT '123',
                s_carid FOR sbook-carrid,
                s_fldat  FOR sbook-fldate.



INITIALIZATION.

AT SELECTION-SCREEN.

START-OF-SELECTION.

perform zf_start.

  SELECT stravelag~agencynum stravelag~name stravelag~currency
         sbook~carrid sbook~connid sbook~fldate sbook~bookid
         sbook~forcuram sbook~forcurkey
         scarr~carrname
         sbook~loccuram sbook~loccurkey
    FROM stravelag JOIN sbook
                     ON stravelag~agencynum = sbook~agencynum
                   JOIN scarr
                     ON sbook~carrid = scarr~carrid
    INTO TABLE gt_output
    WHERE stravelag~agencynum IN s_agnum
    AND   sbook~carrid        IN s_carid
    AND   sbook~fldate        IN s_fldat.

END-OF-SELECTION.

  DESCRIBE TABLE gt_output LINES gv_lines.
  IF gv_lines NE 0.
    SORT gt_output BY agencynum
                      carrid
                      connid
                      fldate
                      bookid.

    PERFORM zf_build_layout USING gs_layout.

    PERFORM zf_build_fieldcatalog USING gt_fieldcat[].

    CALL SCREEN 9100.

  ELSE.
    MESSAGE ID '00' TYPE 'I' NUMBER 001 WITH 'No data retrieved'.
    RETURN.
  ENDIF.


*&---------------------------------------------------------------------*
*&      Form  zf_build_fieldcatalog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->LT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM zf_build_fieldcatalog USING lt_fieldcat TYPE lvc_t_fcat.

  DATA: ls_fieldcat TYPE lvc_s_fcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'AGENCYNUM'.
  ls_fieldcat-ref_table     = 'STRAVELAG'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'NAME'.
  ls_fieldcat-ref_table     = 'STRAVELAG'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'CURRENCY'.
  ls_fieldcat-ref_table     = 'STRAVELAG'.
  APPEND ls_fieldcat TO lt_fieldcat.



  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'CARRID'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'CONNID'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'FLDATE'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'BOOKID'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'FORCURAM'.
  ls_fieldcat-cfieldname    = 'FORCURKEY'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'FORCURKEY'.
  ls_fieldcat-ref_table      = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'CARRNAME'.
  ls_fieldcat-ref_table     = 'SCARR'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'LOCCURAM'.
  ls_fieldcat-cfieldname    = 'LOCCURKEY'.
  ls_fieldcat-ref_table   = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname     = 'LOCCURKEY'.
  ls_fieldcat-ref_table     = 'SBOOK'.
  APPEND ls_fieldcat TO lt_fieldcat.

ENDFORM.                    "zf_build_fieldcatalog

*----------------------------------------------------------------------*
*  MODULE zm_status_9100 OUTPUT
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
MODULE zm_status_9100 OUTPUT.

  DATA: g_dock_container TYPE REF TO cl_gui_docking_container.

  SET PF-STATUS 'MAIN9100'.
  IF g_custom_container IS INITIAL.

*    if cl_gui_alv_grid=>offline() is initial. "online/foreground

    CREATE OBJECT g_custom_container
    EXPORTING
      container_name = g_container.
    CREATE OBJECT grid1
    EXPORTING
      i_parent = g_custom_container.
*    else.        "background
*      create object grid1
*      exporting i_parent = g_dock_container.
*    endif.

    CALL METHOD grid1->set_table_for_first_display
      EXPORTING
        i_structure_name = 'LTY_OUTPUT'
        is_layout        = gs_layout
      CHANGING
        it_fieldcatalog  = gt_fieldcat
        it_outtab        = gt_output.
  ENDIF.
ENDMODULE.                    "zm_status_9100 OUTPUT

*&---------------------------------------------------------------------*
*&      Form  zf_exit_program
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM zf_exit_program.
  LEAVE PROGRAM.
ENDFORM.                    "zf_exit_program
*&---------------------------------------------------------------------*
*&      Module  zm_USER_COMMAND_9100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE zm_user_command_9100 INPUT.
  CALL METHOD cl_gui_cfw=>dispatch.
  CASE ok_code.
    WHEN 'BACK'
      OR 'EXIT'
      OR 'CANC'.
      PERFORM zf_exit_program.
    WHEN OTHERS.
  ENDCASE.

  CLEAR ok_code.

ENDMODULE.                 " zm_USER_COMMAND_9100  INPUT
*&---------------------------------------------------------------------*
*&      Form  zf_build_layout
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GS_LYOUT  text
*----------------------------------------------------------------------*
FORM zf_build_layout  USING ls_layout TYPE lvc_s_layo.
  ls_layout-zebra = 'X'. "Zebra Layout
  ls_layout-cwidth_opt = 'X'.  "optimized column widths
  ls_layout-totals_bef = 'X'. "totals on top
  ls_layout-grid_title = gv_title.

ENDFORM.                    " zf_build_layout
*&---------------------------------------------------------------------*
*&      Form  zf_start
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form zf_start .
* populate screen title using selection variant name
  CONCATENATE 'Airline Bookings:'(001)
              syst-slset
              INTO gv_title SEPARATED BY space.
  IF syst-slset CS '_Z'.
    CONCATENATE gv_title
    '(previous data view)'(002)
          INTO gv_title SEPARATED BY space.
  ENDIF.

endform.                    " zf_start