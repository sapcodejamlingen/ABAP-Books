*&---------------------------------------------------------------------*
*& Report  Z_INHERITANCE_DOWN_CAST_AO
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  z_inheritance_down_cast_ao.

************************************************************************
* Report Name  : Z_INHERITANCE_DOWN_CAST_AO                            *
* Report Title : Down cast in der Vererbung                *
* SAP Release 6.0                                                      *
* Author´s Name: BUESCHERM                                         *
* Create Date  : 01/07/2019 (MM/DD/YYYY)                               *
* Called from  : <Report where used Information>                       *
*                                                                      *
* Description  :  ABAP Objects S 445               *
*              : <DESCRIPTION EXPLAINING FUNCTIONALITY>                *
*              : <DESCRIPTION EXPLAINING FUNCTIONALITY>                *
*              : <DESCRIPTION EXPLAINING FUNCTIONALITY>                *
************************************************************************
* Development / Modification History                                   *
************************************************************************
* Ver.1 | Date:01.07.18 | Author: M.Büscher | Destrcription/Transport  *
************************************************************************

CLASS vehicle DEFINITION DEFERRED.

*----------------------------------------------------------------------*
*       CLASS demo DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS demo DEFINITION.
  PUBLIC SECTION.
    CLASS-METHODS: main,
                   use_vehicle IMPORTING vref
                                TYPE REF TO vehicle.
ENDCLASS.                    "demo DEFINITION



*----------------------------------------------------------------------*
*       CLASS vehicle DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS vehicle DEFINITION.
  PUBLIC SECTION.
    METHODS: accelerate IMPORTING delta TYPE i,
             show_speed.
  PROTECTED SECTION.
    DATA speed TYPE i.
ENDCLASS.                    "vehicle DEFINITION

*----------------------------------------------------------------------*
*       CLASS car DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS car DEFINITION INHERITING FROM vehicle.
  PUBLIC SECTION.
    METHODS show_speed REDEFINITION.
ENDCLASS.                    "car DEFINITION

*----------------------------------------------------------------------*
*       CLASS truck DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS truck DEFINITION INHERITING FROM vehicle.
  PUBLIC SECTION.
    METHODS: accelerate REDEFINITION,
             show_speed REDEFINITION,
             load IMPORTING freight TYPE string,
             unload.
  PROTECTED SECTION.
    DATA freight TYPE string.
  PRIVATE SECTION.
    CONSTANTS max_speed TYPE i VALUE '80'.
ENDCLASS.                    "truck DEFINITION

*----------------------------------------------------------------------*
*       CLASS car IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS car IMPLEMENTATION.
  METHOD show_speed.
    DATA output TYPE string.
    output = me->speed.
    CONCATENATE `Car, speed: ` output INTO output.
    MESSAGE output TYPE 'I'.
  ENDMETHOD.                    "show_speed
ENDCLASS.                    "car IMPLEMENTATION
*----------------------------------------------------------------------*
*       CLASS vehicle IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS vehicle IMPLEMENTATION.
  METHOD accelerate.
    me->speed = me->speed + delta.
  ENDMETHOD.                    "accelerate
  METHOD show_speed.
    DATA output TYPE string.
    output = me->speed.
    MESSAGE output TYPE 'I'.
  ENDMETHOD.                    "show_speed
ENDCLASS.                    "vehicle IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS truck IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS truck IMPLEMENTATION.
  METHOD accelerate.
    super->accelerate( delta ).
    IF me->speed > truck=>max_speed.
      me->speed = truck=>max_speed.
    ENDIF.
  ENDMETHOD.                    "accelerate
  METHOD show_speed.
    DATA output TYPE string.
    output = me->speed.
    CONCATENATE `Truck with ` me->freight
                      `, speed: ` output
                        INTO output.
    MESSAGE output TYPE 'I'.
  ENDMETHOD.                    "show_speed

  METHOD load.
    me->freight = freight.
  ENDMETHOD.                    "load
  METHOD unload.
    CLEAR me->freight.
  ENDMETHOD.                "load
ENDCLASS.                    "truck IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS demo IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS demo IMPLEMENTATION.
  METHOD main.
    DATA: vref1 TYPE REF TO vehicle,
          vref2 TYPE REF TO vehicle.

    CREATE OBJECT: vref1 TYPE car,
    vref2 TYPE truck.
    demo=>use_vehicle( vref1 ).
    demo=>use_vehicle( vref2 ).
  ENDMETHOD.                    "main
  METHOD use_vehicle.
    DATA truck_ref TYPE REF TO truck.
    TRY.
        truck_ref ?= vref.
        truck_ref->load( `Beer` ).
      CATCH cx_sy_move_cast_error.
    ENDTRY.
    vref->accelerate( 130 ).
    vref->show_speed( ).
  ENDMETHOD.                    "use_vehicle
ENDCLASS.                    "demo IMPLEMENTATION



START-OF-SELECTION.
  demo=>main( ).